;
;  Sample GSL file for a VoiceMail application.
;
;  Examples of what you can say:
;  (Pausing between words is not necessary.)
;
;  "Call home."
;
;  "Get me George on the line."
;
;  "Dial extension seven nine oh six."
;
;  "Phone four one five, four six two, eight two zero zero"
;
;  "Dial external three two seven, six eight zero four."
;
;  "Next message."
;
;  "Delete message three."
;
;  "Play message number five."
;

CALL		[phone call dial (get me)]
;DIGIT		[0 1 2 3 4 5 6 7 8 9] {return ($string)}
DIGIT		[zero {return (0)} one {return (1)} two {return (2)} three {return (3)} four {return (4)} five {return (5)} six {return (6)} seven {return (7)} eight {return (8)} nine {return (9)} ] 

SEVEN-DIGIT	(DIGIT DIGIT DIGIT DIGIT DIGIT DIGIT DIGIT)
TEN-DIGIT	(DIGIT DIGIT DIGIT SEVEN-DIGIT)
FOUR-DIGIT	(DIGIT DIGIT DIGIT DIGIT)

PHONE-NUMBER	[
		  ( ?[extension internal] FOUR-DIGIT  {return ($string)} )
		  ( ?[external outside]   [SEVEN-DIGIT TEN-DIGIT] {return ($string)} )
		]

PLACE		[home office police]			{return ($string)}
PERSON		[john paul george susan mary jessica]	{return ($string)}

VMCOMMAND	[save		{<command save_msg>}
		 delete		{<command delete_msg>}
		 play		{<command play_msg>}
		]
RELATIVE	[next last this that]	{return ($string)}
RELATIVE2	[next last]		{return ($string)}
MAILBOX		[
		  (RELATIVE2:which message {<command goto_msg> <which_msg $which>})
		  (VMCOMMAND message ?number DIGIT:which   {<which_msg $which>})
		  (VMCOMMAND RELATIVE:which message	   {<which_msg $which>})
		]

.SENTENCE	([
		  (MAILBOX)
		  (CALL PLACE:recip		{<command dial> <place $recip>} )
		  (CALL PERSON:recip  {<command dial> <person $recip>} ?(on the line) )
		  (CALL PHONE-NUMBER:recip	{<command dial> <number $recip>} )
		] ?please {<polite yes>} )

